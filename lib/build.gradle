plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}




def globalName    = 'linux-gpio'

def globalVersion = '1.0.1'

def globalDesc    = 'Use the standard Linux GPIO API from Java'

def globalUrl     = 'https://github.com/helins/linux-gpio.java'




group   = 'io.helins'
version = globalVersion




dependencies {
    api            'io.helins:linux-common:0.1.4'
    implementation 'io.helins:linux-epoll:1.0.1'
    implementation 'io.helins:linux-errno:1.0.2'
    implementation 'io.helins:linux-io:0.0.4'
    testCompile    'org.junit.jupiter:junit-jupiter-api:5.7.1'
    testRuntime    'org.junit.jupiter:junit-jupiter-engine:5.7.1'
}




test {

    useJUnitPlatform()

    testLogging {

        events 'passed'  ,
               'skipped' ,
               'failed'
    }

    systemProperty( 'onRaspberry'                       ,
                    System.getProperty( 'onRaspberry' ) )
}




java {
    withJavadocJar()
    withSourcesJar()
}




tasks.jar {
    manifest {
        attributes("Automatic-Module-Name": "io.helins.linux.gpio")
    }
}




javadoc {
    options.links( "https://docs.oracle.com/en/java/javase/11/docs/api/" ,
                   "https://javadoc.io/doc/io.helins/linux-common"       )
}




test {

    useJUnitPlatform()

    testLogging {

        events "passed"  ,
               "skipped" ,
               "failed"
    }

    systemProperty( "raspduino"                       ,
                    System.getProperty( "raspduino" ) ) ;
}




// Publishing to Maven


publishing {


    publications {
        LibMaven( MavenPublication ) {

            from components.java


            pom {


                artifactId  = globalName
                name        = globalName
                description = globalDesc
                url         = globalUrl

                licenses {
                     license {
                         name = "Mozilla Public License Version 2.0"
                         url  = "https://www.mozilla.org/en-US/MPL/2.0/"
                     }
                }


                developers {
                    developer {
                        name            = "Adam Helinski"
                        email           = "adam@helins.io"
                        organizationUrl = "http://www.helins.io"
                    }
                }
            
            
                scm {
                    connection          = "scm:git:git://github.com/helins/linux-gpio.java.git"
                    developerConnection = "scm:git:ssh://github.com/helins/linux-gpio.java.git"
                    url                 = globalUrl
                }
            }
        }
    }


    repositories {
        maven {
            credentials( PasswordCredentials )
            name = "sonatype"
            url  = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
        }
    }
}




signing {
    sign publishing.publications.LibMaven
}
